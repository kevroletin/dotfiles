#!/usr/bin/env nu

def pamix [] {
  let idx = job spawn {
    (^alacritty --class=xmonad-center-float
      -o window.dimensions.columns=80 -o window.dimensions.lines=20
      -e pamix)
    "done" | job send 0
  }
  # wait until pamix started and ready to handle input
  let win_id = do {
    mut pid = 0
    loop {
      sleep 100ms
      let pids = job list | where id == $idx | get pids | first
      if ($pids | is-not-empty) {
        $pid = ($pids | first | into int)
        break
      }
    }
    sleep 100ms
    xdotool search --pid $pid | into int
  }
  xdotool key --window $win_id F3
  job recv
}

def modify-transparenty [f] {
  let config = $"($env.HOME)/.config/alacritty/alacritty.toml"
  let template = $"($config).template"
  let opacity = open -r $config | parse "opacity = {x}" | first | get x | default 1.0 | into float
  let res = do $f $opacity
  nu $"($env.HOME)/.xmonad/expand-template" --file $template --vars ({opacity: $res} | to nuon)
}

def toggle-transparenty [] {
  modify-transparenty { |x|
    if ($x >= 1.0) {
      0.85
    } else {
      1
    }
  }
}

def less-transparenty [] { modify-transparenty { |x| if ($x >= 0.95) { 1 } else { $x + 0.05 } } }
def more-transparenty [] { modify-transparenty { |x| if ($x <= 0.5) { 0 } else { $x - 0.05 } } }

let keys = {
  a: {
    group: adjust
    keys: {
      v: [volume
          { || pamix }]
    }
  }
  t: {
    group: toggle
    keys: {
      t: [transparency
          { || toggle-transparenty }]
    }
  }
  "i": {
    group: increase
    keys: {
      t: [transparency
          { || more-transparenty }]
    }
  }
  "d": {
    group: decrease
    keys: {
      t: [transparency
          { || less-transparenty }]
    }
  }
}

let key_list = flatten_keys [] "" "" { group: "", keys: $keys }

def flatten_keys [res acc_key acc_desc value] {
  match ($value | describe -d | get type) {
    "list" => ($res | append {key: $acc_key, desc: $"($acc_desc) ($value | first)", act: ($value | get 1)}),
    "record" => {
      let $group = $value.group
      $value.keys
      | items { |k, v| {key: $k, value: $v} }
      | reduce --fold $res { |$x, fold|
        flatten_keys $fold $"($acc_key)($x.key)" $"($acc_desc) ($group)" $x.value
      }
    }
  }
}

def open_menu [] {
  each { $"($in.key)\t($in.desc)" } | to text | (/usr/bin/rofi
    -dmenu
    -window-title "Which key"
    -cycle
    -matching regex
    -selected-row
    -format i
    -filter "^"
    -auto-select
  )
}

def interact [] {
  let selection = $key_list | open_menu | into int
  $key_list | get $selection
}

interact | do $in.act


