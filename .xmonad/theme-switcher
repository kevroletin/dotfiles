#!/usr/bin/env nu
#
# * rofi - application launcher menu
# * xmobar - status bar
# * trayer - system tray
# * dunst - notifications
# * alacritty - terminal

let HOME = $env.HOME

def set_wallpaper [style] {
  let dir = $"($env.HOME)/Pictures/wallpapers"
  let x = if ($style == light) {
    $"($dir)/light.jpg"
  } else {
    $"($dir)/dark.png"
  }
  print $"feh --bg-scale ($x)"
  feh --bg-scale $x
}

def link_config [file_path, style] {
  let x = $"($HOME)/($file_path)"
  print $"($x).($style)" $x
  ln -f --symbolic --relative $"($x).($style)" $x
}

def set_rofi_theme [style] {
  $'@theme "@($style)"' | write -f .config/rofi/adaptive_theme.rasi
}

# trayer, xmobar, dunst
def expand_templates [style] {
  let themes_dir = $"($env.HOME)/.xmonad/themes/"
  let script = $"($themes_dir)/expand.nu"
  let vars = $"($themes_dir)/vars_($style).nuon"

  nu $script --dir $themes_dir  --vars $vars
}

def set_bunch_of_themes [style] {
  expand_templates $style
}

def set_alacritty_theme [style] {
  let x = $"($HOME)/.config/alacritty"
  let template = $'general.import = [ "($x)/themes_git/themes/solarized_($style).toml" ]'
  $template | save --force $"($x)/theme.toml"
}

def set_theme [style] {
  let current = system_theme

  set_bunch_of_themes $style
  set_alacritty_theme $style
  set_rofi_theme $style

  set_wallpaper $style
}

def system_theme [] {
  if (gsettings get org.gnome.desktop.interface color-scheme | str contains 'dark') {
    'dark'
  } else {
    'light'
  }
}

def restart_deamons [] {
  # restarted by xmobar dependency in systemd
  # systemctl --user restart trayer.service
  systemctl --user restart xmobar.service
  systemctl --user restart dunst.service
}

def "main monitor" [] {
  gsettings monitor org.gnome.desktop.interface color-scheme
      | lines
      | each {|line|
          print $line
          system_theme | set_theme $in
          restart_deamons
          notify-send 'Desktop theme update'
      }
}

def "main set" [new_style] {
  let new_theme = if ($new_style == light) {
      'prefer-light'
    } else if ($new_style == dark) {
      'prefer-dark'
    } else {
      print "expecting 'light' or 'dark'"
      exit 1
    }
  gsettings set org.gnome.desktop.interface color-scheme $new_theme
  set_theme $new_style
}

def "main set-by-time" [] {
  let h = date now | format date "%H" | into int
  if (6 <= $h and $h <= 18) {
      set_theme 'light'
      gsettings set org.gnome.desktop.interface color-scheme 'prefer-light'
  } else {
      set_theme 'dark'
      gsettings set org.gnome.desktop.interface color-scheme 'prefer-dark'
  }
}

def main [] {
}
