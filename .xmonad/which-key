#!/usr/bin/env -S nu --stdin

use nu *
use std/log *

def --wrapped xmonadctl [...$rest] {
  run-external $"($env.HOME)/.local/bin/xmonadctl" ...$rest
}

def pamix [] {
  let idx = job spawn {
    (^alacritty --class=xmonad-center-float
      -o window.dimensions.columns=80 -o window.dimensions.lines=20
      -e pamix)
    "done" | job send 0
  }
  # wait until pamix started and ready to handle input
  let win_id = do {
    mut pid = 0
    loop {
      sleep 100ms
      let pids = job list | where id == $idx | get pids | first
      if ($pids | is-not-empty) {
        $pid = ($pids | first | into int)
        break
      }
    }
    sleep 100ms
    xdotool search --pid $pid | into int
  }
  xdotool key --window $win_id F3
  job recv
}

def modify-transparenty [f] {
  let config = $"($env.HOME)/.config/alacritty/alacritty.toml"
  let template = $"($config).template"
  let opacity = open -r $config | parse "opacity = {x}" | first | get x | default 1.0 | into float
  let res = (do $f ($opacity * 100 | into int)) / 100
  nu $"($env.HOME)/.xmonad/expand-template" --file $template --vars ({opacity: $res} | to nuon)
}

def toggle-transparenty [] {
  modify-transparenty { |current|
    let old = $current | store_key "alacritty.opacity" | default 100
    if ($current >= 100) {
      if ($old < 100) { $old } else { 85 }
    } else {
      100
    }
  }
}

def less-transparenty [] { modify-transparenty { |x| if ($x >= 95) { 100 } else { $x + 05 } } }
def more-transparenty [] { modify-transparenty { |x| if ($x <= 5) { 0 } else { $x - 5 } } }
def set-transparency [val] { modify-transparenty { |_x| $val } }
def less-brightness [] { run-external $"($env.HOME)/bin/adjust-brightness" "-" }
def more-brightness [] { run-external $"($env.HOME)/bin/adjust-brightness" "+" }
def knob_mode [mode] { nu $"($env.HOME)/.xmonad/knob_action" set-mode $mode }
def set_theme [theme] { nu $"($env.HOME)/.xmonad/theme-switcher" set $theme }

let keys = {
  a: {
    group: adjust
    keys: {
      V: ["volume (pamix)" { || pamix }]
      v: [volume { || knob_mode "volume" }]
      t: [transparency { || knob_mode "transparency" }]
      g: [gaps { || knob_mode "gaps" }]
      b: [brightness { || knob_mode "brightness" }]
    }
  }
  c: {
    group: connect
    keys: {
      v: [wifi { || nmcli con up id "Xiaomi 15" }]
    }
  }
  d: {
    group: decrease
    keys: {
      b: [brightness { || less-brightness }]
      g: [gaps { || xmonadctl layout-spacing-inc }]
      t: [transparency { || less-transparenty }]
    }
  }
  i: {
    group: increase
    keys: {
      b: [brightness { || more-brightness }]
      g: [gaps { || xmonadctl layout-spacing-inc }]
      t: [transparency { || more-transparenty }]
    }
  }
  l: {
    group: layout
    keys: {
      l: [next { || xmonadctl "layout-next" }]
      m: ["maximized with gaps" { || xmonadctl "layout-set-sfull" }]
      M: [Maximized { || xmonadctl "layout-set-full" }]
      t: [tall { || xmonadctl "layout-set-stall" }]
      f: ["maximize focused" { || xmonadctl "layout-toggle-focused-maximize" }]
      x: ["toggle float (eXclude from tiling)" { || xmonadctl "layout-toggle-float" }]
      g: {
        group: "gaps"
        keys: {
          "1": ["size 1" { || xmonadctl "layout-spacing-set-1" }]
          "2": ["size 2" { || xmonadctl "layout-spacing-set-2" }]
          "3": ["size 3" { || xmonadctl "layout-spacing-set-3" }]
          "4": ["size 4" { || xmonadctl "layout-spacing-set-4" }]
          "5": ["size 5" { || xmonadctl "layout-spacing-set-5" }]
          t: [toggle { || xmonadctl "layout-spacing-toggle" }]
        }
      }
    }
  }
  s: {
    group: style
    keys: {
      d: [dark { || set_theme dark }]
      D: { 
        group: "dark parameters" 
        keys: {
          b: [black { || set_theme late-dark }]
          s: [sunset { || set_theme sunset-dark }]
        }
      }
      l: [light { || set_theme light }]
      L: {
        group: "light parameters"
        keys: {
          e: [early, { || set_theme early-light }]
        }
      }
      t: {
        group: transparency
        keys: {
          "1": ["preset 1" { || set-transparency 90 }]
          "2": ["preset 2" { || set-transparency 80 }]
          "3": ["preset 3" { || set-transparency 70 }]
          "4": ["preset 4" { || set-transparency 60 }]
          "5": ["preset 5" { || set-transparency 50 }]
          "6": ["preset 1" { || set-transparency 30 }]
          "7": ["preset 2" { || set-transparency 30 }]
          "8": ["preset 3" { || set-transparency 20 }]
          "9": ["preset 4" { || set-transparency 10 }]
          "0": ["preset 5" { || set-transparency 0 }]
          t: [toggle { || toggle-transparenty }]
        }
      }
    }
  }
  t: {
    group: toggle
    keys: {
      g: [gaps { || xmonadctl "layout-spacing-toggle" }]
      t: [transparency { || toggle-transparenty }]
      d: [docks { || run-external $"($env.HOME)/.xmonad/toggle-xmobar" }]
    }
  }
}

let key_list = flatten_keys [] "" "" { group: "", keys: $keys }
let key_txt_lines = $key_list | each { $"($in.key)\t($in.desc)" }

def flatten_keys [res acc_key acc_desc value] {
  match ($value | describe -d | get type) {
    "list" => ($res | append {key: $acc_key, desc: $"($acc_desc) ðŸ¢š ($value | first)", act: ($value | get 1)}),
    "record" => {
      let $group = $value.group
      $value.keys
      | items { |k, v| {key: $k, value: $v} }
      | reduce --fold $res { |$x, fold|
        let combined_name = if ($acc_desc | is-empty) { $group } else { $"($acc_desc) ðŸ¢š ($group)" }
        flatten_keys $fold $"($acc_key)($x.key)" $combined_name $x.value
      }
    }
  }
}

def open_menu [] {
  (/usr/bin/rofi
    -drun-use-desktop-cache
    -dmenu
    -window-title "Which key"
    -cycle
    -matching regex
    -selected-row
    -format i
    -filter "^"
    -auto-select
  )
}

def interact [] {
  let selection = $key_txt_lines | to text | open_menu | into int
  $key_list | get $selection
}

def main [] {
  let res = interact

  $res.key | store_key "which-key.last-action"

  do $res.act
  ignore
}

def "main repeat" [] {
  let last_action = "which-key.last-action" | load_key
  if ($last_action | is-not-empty) {
    $key_list | where key == $last_action | first | each {
      do $in.act
    }
  }
}

def "main list" [] {
  $key_txt_lines | to text
}

def "main continue" [] {
  let inp_key = $in | parse "{keys}\t{_}" | get keys | first
  info $"($inp_key)"
  if ($inp_key | is-not-empty) {
    $key_list | where key == $inp_key | first | each {
      $in.key | store_key "which-key.last-action"
      do $in.act
    }
  }
  $key_txt_lines | to text | save -f $"($env.HOME)/.xmonad/which-key.list.txt"
}


